?tacl routine
==   Copyright (c) 2015 NuWave Technologies, Inc. All rights reserved. (www.nuwavetech.com)

#frame

#push nePathmon
#push console
#push volumes

== Customize these parameters for your system
#set console $zhome
#set volumes $dsmscm, $oss
#set nePathmon $dmne
== End of customization

#output Using Pathmon name [nePathmon]

==  Shutdown the existing pathmon
[#if [#PROCESSEXISTS [nePathmon]] |then|
  #output Shutting down Pathmon [nePathmon] ...
  pathcom [nePathmon] ; shutdown2, mode immediate
  sink / [#stop [nePathmon]] /
]

==  Start the pathmon and give it a few moments to start.
assign pathctl, nepctl

#output Starting pathmon [nePathmon] ...
pathmon / name [nePathmon], &
in [console], &
out [console], &
term [console], &
nowait /
clear assign pathctl

#delay 500

==  Configure the Pathway.

#output Configuring pathmon [nePathmon] ...

pathcom /inline/ [nePathmon]
inlprefix +
+ errors 99
+ set pathway maxassigns        50
+ set pathway maxdefines        50
+ set pathway maxexternaltcps   0
+ set pathway maxlinkmons       16
+ set pathway maxparams         20
+ set pathway maxpathcoms       4
+ set pathway maxprograms       10
+ set pathway maxserverclasses  8
+ set pathway maxserverprocesses 40
+ set pathway maxspi            4
+ set pathway maxstartups       4
+ set pathway maxtcps           2
+ set pathway maxtellqueue      0
+ set pathway maxtells          0
+ set pathway maxterms          10
+ set pathway maxtmfrestarts    0
+ set pathway security          "n"
+ start pathway cold !

+ reset server
+ set server cpus           0:1
+ set server createdelay    0 secs
+ set server deletedelay    10 mins
+ set server highpin        on
+ set server linkdepth      5
+ set server maxservers     1
+ set server maxlinks       20
+ set server numstatic      1
+ set server program        nesvr
+ set server tmf            off
+ set server debug          off
+ set server in             [console]
+ set server out            [console]
+ set server param          volumes "[volumes]"

== set maxcpus to the number of installed cpus
+ set server param          maxcpus 4

== set interval to the cpu status interval in hsecs
+ set server param          interval 200

+ add server nesvr

+ start server *

inleof

#unframe
